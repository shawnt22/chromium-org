# Copyright 2025 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")

assert(!is_android)

source_set("actor") {
  friend = [
    ":unit_tests",
    ":browser_tests",
  ]
  public = [
    "actor_keyed_service.h",
    "actor_keyed_service_factory.h",
    "actor_task.h",
    "aggregated_journal.h",
    "aggregated_journal_file_serializer.h",
    "aggregated_journal_in_memory_serializer.h",
    "aggregated_journal_serializer.h",
    "execution_engine.h",
    "tools/observation_delay_controller.h",
    "tools/tool_controller.h",
    "tools/tool_request.h",
  ]
  public_deps = [
    ":types",
    "//base",
    "//chrome/browser/profiles",
    "//chrome/common",
    "//components/optimization_guide/content/browser",
    "//components/optimization_guide/proto:optimization_guide_proto",
    "//components/tabs:public",
    "//content/public/browser",
  ]
  if (enable_glic) {
    public_deps += [ "//chrome/browser/glic:glic" ]
  }
}

source_set("impl") {
  sources = [
    "actor_features.cc",
    "actor_features.h",
    "actor_keyed_service.cc",
    "actor_keyed_service_factory.cc",
    "actor_task.cc",
    "aggregated_journal.cc",
    "aggregated_journal_file_serializer.cc",
    "aggregated_journal_in_memory_serializer.cc",
    "aggregated_journal_serializer.cc",
    "browser_action_util.cc",
    "browser_action_util.h",
    "execution_engine.cc",
    "site_policy.cc",
    "site_policy.h",
    "tools/click_tool_request.cc",
    "tools/click_tool_request.h",
    "tools/drag_and_release_tool_request.cc",
    "tools/drag_and_release_tool_request.h",
    "tools/history_tool.cc",
    "tools/history_tool.h",
    "tools/history_tool_request.cc",
    "tools/history_tool_request.h",
    "tools/move_mouse_tool_request.cc",
    "tools/move_mouse_tool_request.h",
    "tools/navigate_tool.cc",
    "tools/navigate_tool.h",
    "tools/navigate_tool_request.cc",
    "tools/navigate_tool_request.h",
    "tools/observation_delay_controller.cc",
    "tools/page_tool.cc",
    "tools/page_tool.h",
    "tools/page_tool_request.cc",
    "tools/page_tool_request.h",
    "tools/scroll_tool_request.cc",
    "tools/scroll_tool_request.h",
    "tools/select_tool_request.cc",
    "tools/select_tool_request.h",
    "tools/tab_management_tool.cc",
    "tools/tab_management_tool.h",
    "tools/tab_management_tool_request.cc",
    "tools/tab_management_tool_request.h",
    "tools/tool.cc",
    "tools/tool.h",
    "tools/tool_callbacks.cc",
    "tools/tool_callbacks.h",
    "tools/tool_controller.cc",
    "tools/tool_request.cc",
    "tools/type_tool_request.cc",
    "tools/type_tool_request.h",
    "tools/wait_tool.cc",
    "tools/wait_tool.h",
    "tools/wait_tool_request.cc",
    "tools/wait_tool_request.h",
  ]

  public_deps = [ "//chrome/browser:browser_public_dependencies" ]

  deps = [
    ":actor",
    "//chrome/browser:browser_process",
    "//chrome/browser/actor/ui:event_dispatcher",
    "//chrome/browser/optimization_guide:optimization_guide",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/safe_browsing",
    "//chrome/browser/ui:browser_navigator_params_headers",
    "//chrome/browser/ui/browser_window:browser_window",
    "//chrome/browser/ui/tabs:tabs_public",
    "//chrome/common",
    "//components/lookalikes/core",
    "//components/safe_browsing:buildflags",
    "//components/safe_browsing/core/common:safe_browsing_prefs",
    "//components/variations/service:service",
    "//content/public/browser",
    "//net",
    "//third_party/perfetto:libperfetto",
    "//third_party/perfetto/protos/perfetto/config:zero_gen",
    "//third_party/perfetto/protos/perfetto/trace/perfetto:zero_gen",
    "//url",
  ]
  if (enable_glic) {
    deps += [ "//chrome/browser/glic:glic" ]
  }
}

# This target contains only type definitions that may be depended on by other
# headers without depending on all of :actor.
# This avoids dependency cycles.
source_set("types") {
  sources = [ "task_id.h" ]
  public_deps = [ "//base" ]
}

source_set("test_support") {
  testonly = true
  sources = [
    "actor_test_util.cc",
    "actor_test_util.h",
  ]

  deps = [ ":actor" ]

  public_deps = [
    "//base",
    "//components/optimization_guide/proto:optimization_guide_proto",
    "//content/test:test_support",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "actor_keyed_service_unittest.cc",
    "aggregated_journal_unittest.cc",
    "site_policy_unittest.cc",
  ]
  deps = [
    ":actor",
    ":impl",
    ":test_support",
    "ui:event_dispatcher",
    "ui:mock_event_dispatcher",
    "//base/test:test_support",
    "//chrome/browser/optimization_guide:test_support",
    "//chrome/test:test_support",
    "//components/tabs:test_support",
    "//testing/gtest",
  ]
  if (enable_glic) {
    sources += [ "execution_engine_unittest.cc" ]
  }
}

source_set("browser_tests") {
  testonly = true
  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  sources = [ "site_policy_browsertest.cc" ]
  deps = [
    ":actor",
    ":impl",
    ":test_support",
    "//base/test:test_support",
    "//chrome/browser/optimization_guide:test_support",
    "//chrome/browser/safe_browsing",
    "//chrome/browser/ui:ui",
    "//chrome/browser/ui:ui_features",
    "//chrome/browser/ui/zoom:zoom",
    "//chrome/test:test_support",
    "//chrome/test:test_support_ui",
    "//components/optimization_guide/core:bloomfilter",
    "//components/safe_browsing:buildflags",
    "//components/safe_browsing/core/common:features",
    "//components/safe_browsing/core/common:safe_browsing_prefs",
    "//testing/gtest",
  ]

  if (enable_glic) {
    sources += [
      "execution_engine_browsertest.cc",
      "tools/page_stability_browsertest.cc",
      "tools/tools_browsertest.cc",
    ]
    deps += [ "//chrome/browser/glic:glic" ]
  }
}
