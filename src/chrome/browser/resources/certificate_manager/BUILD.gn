# Copyright 2018 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//crypto/features.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//ui/webui/resources/tools/build_webui.gni")

assert(chrome_root_store_cert_management_ui)

mojom("mojom") {
  sources = [ "certificate_manager.mojom" ]
  public_deps = [ "//mojo/public/mojom/base" ]
  webui_module_path = "/"
}

build_webui("build") {
  grd_prefix = "certificate_manager"

  static_files = [
    "certificate_manager_dialog.html",
    "favicon.svg",
  ]
  web_component_files = [
    "certificate_entry.ts",
    "certificate_list.ts",
    "certificate_manager.ts",
    "certificate_subpage.ts",
    "crs_section.ts",
    "local_certs_section.ts",
  ]
  ts_files = [
    "certificate_confirmation_dialog.html.ts",
    "certificate_confirmation_dialog.ts",
    "certificate_info_dialog.html.ts",
    "certificate_info_dialog.ts",
    "certificate_password_dialog.html.ts",
    "certificate_password_dialog.ts",
    "certificates_browser_proxy.ts",
    "navigation.ts",
  ]

  mojo_files_deps = [ ":mojom_ts__generator" ]
  mojo_files = [ "$target_gen_dir/certificate_manager.mojom-webui.ts" ]

  icons_html_files = [ "certificate_manager_icons.html" ]
  css_files = [ "certificate_manager_style.css" ]

  if (is_chromeos) {
    web_component_files += [
      "certificate_provisioning_details_dialog.ts",
      "certificate_provisioning_entry.ts",
      "certificate_provisioning_list.ts",
    ]
    css_files += [ "certificate_shared.css" ]
    ts_files += [
      "certificate_provisioning_browser_proxy.ts",
      "certificate_manager_types.ts",
    ]
  }

  ts_definitions = [
    "//tools/typescript/definitions/chrome_send.d.ts",
    "//tools/typescript/definitions/metrics_private.d.ts",
  ]
  ts_composite = true
  ts_deps = [
    "//third_party/lit/v3_0:build_ts",
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources/cr_elements:build_ts",
    "//ui/webui/resources/js:build_ts",
    "//ui/webui/resources/mojo:build_ts",
  ]

  webui_context_type = "relative"
}
